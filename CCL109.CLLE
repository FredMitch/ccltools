/*********************************************************************/
/*                                                                   */
/*  SYSTEM NAME..: CCL400                                            */
/*                                                                   */
/*  PROGRAM NAME.: CCL109                                            */
/*                                                                   */
/*  PROGRAM DESC.: DISPLAY KEY FIELDS                                */
/*                                                                   */
/*  DATE.........: 17/07/98                                          */
/*                                                                   */
/*  AUTHOR.......: FRED MITCHELL                                     */
/*                                                                   */
/*               (C) COPYRIGHT CONCISE CONSULTING LTD.               */
/*********************************************************************/
             PGM        PARM(&FILEFULL &RETCDE)
/*                                                                   */
/* DECLARATIONS                                                      */
             DCL        &FILEFULL *CHAR LEN(20)
             DCL        &FILE *CHAR LEN(10)
             DCL        &LIB *CHAR LEN(10)
             DCL        &RETCDE *CHAR LEN(1)
             DCL        VAR(&LOGICAL) TYPE(*CHAR) LEN(1)
             DCL        &TEXT *CHAR LEN(50)
             DCL        VAR(&OBJATR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(256)
/*                                                                   */
/* Global message monitor                                            */
             MONMSG     MSGID(CPF0000 MCH0000) EXEC(GOTO CMDLBL(ERROR))
/*                                                                   */
/* EXTRACT FILE/LIBRARY NAMES AND CHECK EXISTENCE                    */
             CHGVAR     &FILE %SST(&FILEFULL 1 10) /* File name */
             CHGVAR     &LIB %SST(&FILEFULL 11 10) /* Library name */
             CHKOBJ     OBJ(&LIB/&FILE) OBJTYPE(*FILE)
/*                                                                   */
/* RETRIEVE OBJECT TEXT/ATTRIBUTES                                   */
             RTVOBJD    OBJ(&LIB/&FILE) OBJTYPE(*FILE) +
                          OBJATR(&OBJATR) TEXT(&TEXT)
/*                                                                   */
/* RETRIEVE KEY                                                      */
             DSPFD      FILE(&LIB/&FILE) TYPE(*ACCPTH) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/CCL109W)
/*                                                                   */
/* RETRIEVE SELECT/OMIT STATEMENTS                                   */
             CHGVAR     VAR(&LOGICAL) VALUE('N')
             IF         COND(&OBJATR = 'LF        ') THEN(DO)
             DSPFD      FILE(&LIB/&FILE) TYPE(*SELECT) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/CCL109X)
             CHGVAR     VAR(&LOGICAL) VALUE('Y')
             ENDDO
/*                                                                   */
/* EXECUTE OVERRIDES AND CALL RPG PROGRAM                            */
             OVRDBF     FILE(CCL109W) TOFILE(QTEMP/CCL109W)
             OVRDBF     FILE(CCL109X) TOFILE(QTEMP/CCL109X)
             CALL       PGM(CCL110) PARM(&TEXT &LOGICAL &RETCDE)
/*                                                                   */
/* Normal end of program                                             */
             RETURN
/*                                                                   */
/* Abnormal end of program                                           */

 ERROR:      RCVMSG     MSGTYPE(*DIAG) MSGDTA(&MSGDTA) MSGID(&MSGID)
             IF         COND(&MSGID *EQ '       ') THEN(GOTO +
                          CMDLBL(ERROR_ESC))
             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(&MSGDTA) +
                          MSGTYPE(*DIAG)
             MONMSG     MSGID(CPF0000 MCH0000)
             GOTO       CMDLBL(ERROR)

/* Re-send escape message and RETURN                                 */
 ERROR_ESC:  RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSGDTA) MSGID(&MSGID)
             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(&MSGDTA) +
                          MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000 MCH0000)
             RETURN

             ENDPGM
/*********************************************************************/
